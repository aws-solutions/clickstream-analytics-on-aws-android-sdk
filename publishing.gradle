/*
 * Copyright 2022 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 *  http://aws.amazon.com/apache2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'

version = project.ext.VERSION_NAME
group = POM_GROUP

afterEvaluate { project ->
    publishing {
        publications {
            println "publish ${project.name}"
            library(MavenPublication) {
                groupId POM_GROUP
                artifactId POM_ARTIFACT_ID
                version project.ext.VERSION_NAME
                artifact("${buildDir}/outputs/aar/${artifactId}-release.aar")
                println "publish ${buildDir}/outputs/aar/${artifactId}-release.aar"
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    // Note that this only handles implementation
                    // dependencies. In the future, may need to add api,
                    // etc.
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        repositories {
            def keyFile = rootProject.file("key.properties")
            def keyProp = new Properties()
            keyProp.load(keyFile.newInputStream())
            maven { // Add Gitlab repo for aws-analytics-web-analytics
                url keyProp.getProperty("projectUrl")
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = keyProp.getProperty("gitLabPrivateToken")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}
